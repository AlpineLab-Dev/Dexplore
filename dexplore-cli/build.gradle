import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath depends.shadow_plugin
        classpath (depends.proguard) {
            exclude group: 'com.android.tools.build'
        }
        classpath depends.kotlin_plugin
    }
}

plugins {
    id 'java'
}
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation depends.jcommander
    implementation depends.jadx_core
    implementation depends.jadx_plugin
    implementation project(':dexplore-lib')
}

task unitTest(type: Test) {
    dependsOn test
}

jar { // noinspection GroovyAssignabilityCheck
    manifest {
        attributes 'Main-Class': "${projectGroup}.dexplore.CommandLine"
        attributes 'Specification-Title': projectName
        attributes 'Specification-Version': projectVersion
        attributes 'Implementation-Title': projectName
        attributes 'Implementation-Version': projectVersion
        attributes 'Build-Time': new Date().format("dd-MM-yyyy hh:mm:ss aa")
    }
    archiveBaseName.set(projectName)
    from fileTree(dir: 'src/main', includes: ['assets/**'])
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from(rootProject.projectDir) {
        include 'LICENSE'
        into('META-INF')
    }
    exclude 'META-INF/*.MF', 'META-INF/*.txt', 'META-INF/NOTICE*',
            'META-INF/**/*.class', 'META-INF/*.kotlin*', 'NOTICE*'
    destinationDirectory = file(jarOutputPath)
}

shadowJar {
    from(rootProject.projectDir) {
        include 'LICENSE'
        into('META-INF')
    }
    exclude 'META-INF/*.MF', 'META-INF/*.txt', 'META-INF/NOTICE*',
            'META-INF/**/*.class', 'META-INF/*.kotlin*', 'NOTICE*'
}

task proguard(type: ProGuardTask, dependsOn: shadowJar) {
    injars shadowJar.getArchiveFile(),
           filter: "!com/google/gson/**,!export/**.tmpl," +
                   "!com/android/aapt/**,!android/aapt/**," +
                   "!com/google/protobuf/**,!google/protobuf/**,!**.proto"
    outjars jar.archiveFile.get().asFile

    def home = System.getProperty("java.home")
    if (!JavaVersion.current().isJava9Compatible()) {
        libraryjars home + '/lib/rt.jar'
    } else {
        libraryjars home + '/jmods', jarfilter: '!**.jar', filter: '!module-info.class'
    }

    dontoptimize
    dontwarn 'org.slf4j.**'
    dontwarn 'com.android.aapt.**'
    dontwarn 'com.google.gson.**'
    dontwarn 'com.google.protobuf.**'
    dontwarn 'org.jf.util.jcommander.**'
    dontwarn 'org.jetbrains.annotations.ApiStatus*'
    keepattributes '*Annotation*'
    keep 'class jadx.api.JadxDecompiler'
    keep 'class jadx.plugins.** { *; }'
    keep 'class com.beust.jcommander.** { *; }'
    keep 'class io.github.neonorbit.dexplore.** { *; }'
    keepclassmembers 'enum * {                                       \
                          public static **[] values();                \
                          public static ** valueOf(java.lang.String);  \
                      }'
}

build.dependsOn(proguard)
